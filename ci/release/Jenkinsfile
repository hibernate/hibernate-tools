/*
 * Copyright 2010 - 2025 Red Hat, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" basis,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
@Library('hibernate-jenkins-pipeline-helpers') _

import org.hibernate.jenkins.pipeline.helpers.version.Version

env.PROJECT = "HBX"
def RELEASE_ON_SCHEDULE = false // Set to `true` *only* on branches where you want a scheduled release.

// Avoid running the pipeline on branch indexing
if (currentBuild.getBuildCauses().toString().contains('BranchIndexingCause')) {
	print "INFO: Build skipped due to trigger being Branch Indexing"
	currentBuild.result = 'NOT_BUILT'
	return
}

def manualRelease = currentBuild.getBuildCauses().toString().contains( 'UserIdCause' )
def cronRelease = currentBuild.getBuildCauses().toString().contains( 'TimerTriggerCause' )

// Only do automatic release on branches where we opted in
if ( !manualRelease && !cronRelease ) {
	print "INFO: Build skipped because automated releases on push are disabled on this branch."
	currentBuild.result = 'NOT_BUILT'
	return
}

if ( !manualRelease && cronRelease && !RELEASE_ON_SCHEDULE ) {
	print "INFO: Build skipped because automated releases are disabled on this branch. See constant RELEASE_ON_SCHEDULE in ci/release/Jenkinsfile"
	currentBuild.result = 'NOT_BUILT'
	return
}

def checkoutReleaseScripts() {
	dir('.release/scripts') {
		checkout scmGit(branches: [[name: '*/main']], extensions: [],
				userRemoteConfigs: [[credentialsId: 'ed25519.Hibernate-CI.github.com',
									 url: 'https://github.com/hibernate/hibernate-release-scripts.git']])
	}
}

pipeline {
	agent {
		label 'Release'
	}
	triggers {
		// Run every week Monday midnight
		cron('0 0 * * 1')
	}
	tools {
		maven 'Apache Maven 3.9'
		jdk 'OpenJDK 17 Latest'
	}
	options {
		buildDiscarder logRotator(daysToKeepStr: '30', numToKeepStr: '10')
		disableConcurrentBuilds(abortPrevious: false)
	}
	parameters {
		string(
				name: 'RELEASE_VERSION',
				defaultValue: '',
				description: 'The version to be released, e.g. 7.2.2.Final.',
				trim: true
		)
		string(
				name: 'DEVELOPMENT_VERSION',
				defaultValue: '',
				description: 'The next version to be used after the release, e.g. 7.2.3-SNAPSHOT.',
				trim: true
		)
		booleanParam(
				name: 'RELEASE_DRY_RUN',
				defaultValue: true,
				description: 'If true, just simulate the release, without pushing any commits or tags, and without uploading any artifacts or documentation.'
		)
	}
	stages {
		stage('Release') {
			steps {
				script {
					print "INFO: params.RELEASE_VERSION = ${params.RELEASE_VERSION}"
					print "INFO: params.DEVELOPMENT_VERSION = ${params.DEVELOPMENT_VERSION}"
					print "INFO: params.RELEASE_DRY_RUN? = ${params.RELEASE_DRY_RUN}"

					checkoutReleaseScripts()

					def currentVersion = Version.parseDevelopmentVersion( sh(
							script: ".release/scripts/determine-current-version.sh ${env.PROJECT}",
							returnStdout: true
					).trim() )
					echo "Workspace version: ${currentVersion}"

					def releaseVersion
					def developmentVersion

					if ( manualRelease ) {
						echo "Release was requested manually"

						if ( !params.RELEASE_VERSION ) {
							throw new IllegalArgumentException(
									'Missing value for parameter RELEASE_VERSION. This parameter must be set explicitly to prevent mistakes.'
							)
						}
						releaseVersion = Version.parseReleaseVersion( params.RELEASE_VERSION )

						if ( !releaseVersion.toString().startsWith( currentVersion.family + '.' ) ) {
							throw new IllegalArgumentException( "RELEASE_VERSION = $releaseVersion, which is different from the family of CURRENT_VERSION = $currentVersion. Did you make a mistake?" )
						}
					}
					else {
						echo "Release was triggered automatically"

						def ormVersion = Version.parseReleaseVersion( sh(
								script: "./mvnw help:evaluate -Dexpression=hibernate-orm.version -q -DforceStdout",
								returnStdout: true
						).trim() )

						if (!ormVersion.unqualified().equals(currentVersion.unqualified())) {
							print "INFO: Build skipped because the Hibernate ORM update was not detected in the current sources. Hibernate ORM version: ${ormVersion}; Hibernate Tools version: ${currentVersion}."
							currentBuild.getRawBuild().getExecutor().interrupt(Result.NOT_BUILT)
							sleep(1)   // Interrupt is not blocking and does not take effect immediately.
							return
						}

						releaseVersion = Version.parseReleaseVersion( sh(
								script: ".release/scripts/determine-release-version.sh ${currentVersion}",
								returnStdout: true
						).trim() )
					}
					echo "Release version: ${releaseVersion}"

					if ( !params.DEVELOPMENT_VERSION ) {
						developmentVersion = Version.parseDevelopmentVersion( sh(
								script: ".release/scripts/determine-development-version.sh ${releaseVersion}",
								returnStdout: true
						).trim() )
					}
					else {
						developmentVersion = Version.parseDevelopmentVersion( params.DEVELOPMENT_VERSION )
					}
					echo "Development version: ${developmentVersion}"

					env.RELEASE_VERSION = releaseVersion.toString()
					env.DEVELOPMENT_VERSION = developmentVersion.toString()
					env.JRELEASER_DRY_RUN = params.RELEASE_DRY_RUN

					echo "Performing full release for version ${releaseVersion.toString()}"

					withMaven(mavenSettingsConfig: params.RELEASE_DRY_RUN ? null : 'ci-hibernate.deploy.settings.maven',
							mavenLocalRepo: env.WORKSPACE_TMP + '/.m2repository') {
						configFileProvider([configFile(fileId: 'release.config.ssh', targetLocation: env.HOME + '/.ssh/config'),
											configFile(fileId: 'release.config.ssh.knownhosts', targetLocation: env.HOME + '/.ssh/known_hosts')]) {
							// using MAVEN_GPG_PASSPHRASE (the default env variable name for passphrase in maven gpg plugin)
							withCredentials([file(credentialsId: 'release.gpg.private-key', variable: 'RELEASE_GPG_PRIVATE_KEY_PATH'),
											 string(credentialsId: 'release.gpg.passphrase', variable: 'JRELEASER_GPG_PASSPHRASE'),
											 usernamePassword(credentialsId: 'central.sonatype.com', passwordVariable: 'JRELEASER_MAVENCENTRAL_TOKEN', usernameVariable: 'JRELEASER_MAVENCENTRAL_USERNAME'),
											 usernamePassword(credentialsId: 'gradle-plugin-portal-api-key', passwordVariable: 'GRADLE_PUBLISH_SECRET', usernameVariable: 'GRADLE_PUBLISH_KEY'),
											 string(credentialsId: 'Hibernate-CI.github.com', variable: 'JRELEASER_GITHUB_TOKEN')]) {
								sshagent(['ed25519.Hibernate-CI.github.com', 'hibernate-ci.frs.sourceforge.net']) {
									sh 'cat $HOME/.ssh/config'
									sh """
										bash -xe .release/scripts/release.sh -j ${params.RELEASE_DRY_RUN ? '-d' : ''} \
												tools ${releaseVersion.toString()} ${developmentVersion.toString()}
									"""

									echo "Updating Hibernate ORM/Tools versions in the guides/tutorials."

									sh "sed -i \"s/\\(hibernate.tools.version\\\" value=\\\"\\)[^\\\"]*/\\1the-hibernate-tools-version-to-use, e.g. ${releaseVersion.toString()}/\" ant/docs/5-minute-tutorial.md"
									sh "sed -i \"s/\\(hibernate.tools.version\\\" value=\\\"\\)[^\\\"]*/\\1${releaseVersion.toString()}/\" ant/docs/reference-guide.md"
									sh "sed -i \"s/\\(id 'org.hibernate.tool.hibernate-tools-gradle' version '\\)[^\\']*/\\1${releaseVersion.toString()}/\" gradle/docs/5-minute-tutorial.md"
									sh "sed -i \"s/\\(id 'org.hibernate.tool.hibernate-tools-gradle' version '\\)[^\\']*/\\1${releaseVersion.toString()}/\" gradle/docs/examples/5-minute-tutorial/app/build.gradle"

									sh "git add ant/docs/5-minute-tutorial.md ant/docs/reference-guide.md gradle/docs/5-minute-tutorial.md gradle/docs/examples/5-minute-tutorial/app/build.gradle"
									sh "git commit -m \"[Jenkins release job] Update documentation versions by release build ${releaseVersion.toString()}\""
									sh "git push origin HEAD:${env.GIT_BRANCH}"
								}
							}
						}
					}
				}
			}
		}
	}
	post {
		always {
			notifyBuildResult notifySuccessAfterSuccess: true, maintainers: 'koen@hibernate.org'
		}
	}
}
